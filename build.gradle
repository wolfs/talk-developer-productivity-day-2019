buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'org.asciidoctor:asciidoctor-gradle-plugin:1.5.10'
        classpath 'com.github.jruby-gradle:jruby-gradle-plugin:1.6.0'
        classpath 'org.ysb33r.gradle:vfs-gradle-plugin:1.0.1'
        classpath 'commons-httpclient:commons-httpclient:3.1'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
        classpath('io.ratpack:ratpack-groovy:1.6.0') {
            exclude module: 'groovy-all'
        }
    }

}

plugins {
    id "com.gradle.build-scan" version "2.1"
}

repositories {
    jcenter()
}

apply plugin: 'com.github.jruby-gradle.base'
apply plugin: 'org.ysb33r.vfs'
apply plugin: 'java'
apply plugin: 'org.asciidoctor.convert'

dependencies {
    gems 'rubygems:slim:2.1.0'
    gems 'rubygems:thread_safe:0.3.4'
}

ext {
    downloadDir = new File(buildDir, 'download')
    templateDir = new File(downloadDir, 'templates')
    revealJsDir = new File(downloadDir, 'reveal.js')
}

asciidoctorj {
   version = '1.5.8.1'
}

apply from: 'gradle/pdf.gradle'

task download {
    doLast {
        mkdir downloadDir
        mkdir templateDir
        vfs {
            cp "zip:https://github.com/asciidoctor/asciidoctor-reveal.js/archive/v${asciidoctorBackendVersion}.zip!asciidoctor-reveal.js-${asciidoctorBackendVersion}",
                    templateDir,
                    recursive: true,
                    overwrite: true
            cp "zip:https://github.com/hakimel/reveal.js/archive/${revealjsVersion}.zip!reveal.js-${revealjsVersion}/",
                    revealJsDir,
                    recursive: true,
                    overwrite: true
        }
    }
}

task copyTheme(type: Copy) {
    dependsOn download
    from fileTree("$revealJsDir/reveal.js-${revealjsVersion}")
    from(fileTree('src/docs/theme')) {
        into 'css/theme'
    }
    into revealJsDir
}

task copyScreencasts(type: Copy) {
    from fileTree('src/docs/asciidoc/screencast')
    into file("$buildDir/asciidoc/revealjs")
}

download {
    description "Download extra deckjs/reveal.js resources"
    inputs.property 'asciidoctorBackendVersion', asciidoctorBackendVersion
    inputs.property 'revealjsVersion', revealjsVersion
    outputs.dir templateDir
    outputs.dir revealJsDir
}

apply from: 'gradle/asciidoctor.gradle'

allprojects {
    apply plugin: 'idea'
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

